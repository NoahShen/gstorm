apply plugin: 'groovy'
apply plugin: 'maven-publish'

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
  }
}
apply plugin: 'bintray'

group = 'gstorm'
version = '0.5-SNAPSHOT'
description = "Groovy Single Table ORM"

repositories {
    jcenter()
}

sourceSets {
    main.groovy.srcDir 'src'
    test.groovy.srcDir 'test'
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.2.1',
            'org.hsqldb:hsqldb:2.2.9'
    testCompile 'junit:junit:4.10'
}

publishing {
    publications {
        gstormMaven(MavenPublication) {
            from components.java
        }
    }
}

// set bintrayUser & bintrayKey in gradle.properties
bintray {
    user = getPropertyOrUseDefault("bintrayUser", "fake_user")
    key = getPropertyOrUseDefault("bintrayKey", "fake_key")
    publications = ['gstormMaven']

    def projectName = project.name
    def projectDescription = project.description

    pkg {
        repo = 'maven'
        name = projectName          // somehow project.* doesn't work in this closure
        desc = projectDescription
        licenses = ['Apache-2.0']
    }
    dryRun = true // whether to run this as dry-run, without deploying
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}

String getPropertyOrUseDefault(String propertyName, String defaultValue) {
    hasProperty(propertyName) ? getProperty(propertyName) : defaultValue
}