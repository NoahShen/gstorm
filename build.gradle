apply plugin: 'groovy'

group = 'gstorm'
version = '0.5'
description = "Groovy Single Table ORM"

sourceSets {
    main.groovy.srcDir 'src'
    test.groovy.srcDir 'test'
}

repositories {
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.2.1'
    testCompile 'junit:junit:4.10',
                'org.hsqldb:hsqldb:2.2.9'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
    }
}

apply plugin: 'maven-publish'

publishing {
    publications {
        gstormMaven(MavenPublication) {
            from components.java
        }
    }
}

apply plugin: 'bintray'

// set bintrayUser & bintrayKey in gradle.properties
bintray {
    user = getPropertyOrUseDefault("bintrayUser", "fake_user")
    key = getPropertyOrUseDefault("bintrayKey", "fake_key")
    publications = ['gstormMaven']

    def projectName = project.name
    def projectDescription = project.description

    pkg {
        repo = 'maven'
        name = projectName          // somehow project.* doesn't work in this closure
        desc = projectDescription
        licenses = ['MIT']
    }
    dryRun = true // whether to run this as dry-run, without deploying
}

String getPropertyOrUseDefault(String propertyName, String defaultValue) {
    hasProperty(propertyName) ? getProperty(propertyName) : defaultValue
}
